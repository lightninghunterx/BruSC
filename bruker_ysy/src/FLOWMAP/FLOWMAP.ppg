;****************************************************************
;
; Copyright (c) 2017
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
; 
; FLOWMAP
;
;****************************************************************

#include <MRI.include>
#include <PrepModulesHead.mod>

define delay denab

define list<phase> phaselist = {$RFPhaseList}
define list<frequency> freqTx = {$ACQ_O1_list}
define list<frequency> freqRx = {$ACQ_O1B_list}

"denab =  d3 - de"

INIT_DEVICES

#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2
#define fg FlowEncGradientAmp
#define tw TwisterGradStrength
define list<grad_scalar> fr={$FlowGradR}
define list<grad_scalar> fp={$FlowGradP}
define list<grad_scalar> fs={$FlowGradS}


grad_zero_for_gs <r2d, r3d>

;----------------------- preparation modules -----------------------
            subr FlowSatInit()
            subr SatTransInit()
start,  0u

            subr TriggerPhase()

;-------------------- start of the main loop -----------------------
slice, 	5u freqRx(receive):f1

            subr TriggerSlice()

movie,      UPDATE_DYN_PARS_30u
        5u
            subr FlowSat()
            subr FovSat()
            subr SatTrans()
            subr FatSup()

        d6       grad_ramp{0, 0, g9}

;------------------------ slice selection --------------------------
        d3       grad_ramp{0, 0, g0} freqTx:f1
       (p0:sp0   phaselist):f1
        d4       grad_off

;----------- slice rephase, read dephase, phase encoding -----------
       d10       grad_shape{RShape()*100 + EShape()*fr, P2Shape()*100*r2d + EShape()*fp, SShape()*100 - P3Shape()*100*r3d + EShape()*fs + tw} 

;----------------------- frequency encoding ------------------------
     denab       grad_ramp{g5, 0, 0}

	         ADC_INIT_(job0, ph1, phaselist)

  AQ_(job0)      ADC_START_(job0)

;------------------ read spoiler + phase encoding ------------------
       d11       grad_ramp{g6, r2d*g7, r3d*g8}     ;2nd and 3rd dims
       d12       grad_ramp{g6, 0, 0}
        d3	 grad_off
        d0	 ADC_END_(job0)

;---------------------- slice and movie loop -----------------------
     lo to movie times l10

   if(FlowMode != FourierFlowImaging)
   {
        0u       fr.inc                ;increment flow encoding grad
        0u       fp.inc 
        0u       fs.inc		
     lo to slice times FlowEncLoop
   } 
		
            subr FlowSatInc()
        0u       grad_matrix.inc
        0u       freqTx.inc
        0u       freqRx.inc

     lo to slice times NSLICES

        0u       phaselist.inc          ;phase list for RF spoiling

;--------------------------- dummy loop ----------------------------

                  "l3=l3-1"
   if "l3>=0" goto start

;------------------------- averaging loop --------------------------
     lo to start times NA

;----------------------------- 2d loop -----------------------------
	0u       r2d.inc
     lo to start times l1

;----------------------------- 3d-loop -----------------------------
   if(FlowMode == FourierFlowImaging)
   {
	0u       fr.inc 
        0u       fp.inc 
        0u       fs.inc	
   }
   else
   {
        0u	 r3d.inc
   }

     lo to start times l2

;---------------------- motion averaging loop ----------------------
  
     lo to start times NAE

;-------------------------- sattrans loop --------------------------

            subr SatTransInc()

;------------------------- repetition loop -------------------------
            subr Evolution()

     lo to start times NR

SETUP_GOTO(start)

exit

ph0 = 0
ph1 = 0 




	





