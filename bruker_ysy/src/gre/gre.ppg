;===============================================================================
;=
;= $Source$
;=
;= A gradient echo imaging pulse program
;=
;= $Id$
;===============================================================================

; l0: loop counter for 2d loop
; l1: loop counter for 3d loop
; l2: loop counter for dummy loop

#include <MRI.include>

;===============================================================================
;   definition of delays and frequency lists
;===============================================================================

define delay denab
"denab =  d4 - de"

define list<frequency> freqTx = {$ACQ_O1_list}
define list<frequency> freqRx = {$ACQ_O1B_list}

INIT_DEVICES

;===============================================================================
;   declaration of 2d and 3d loop
;===============================================================================

#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2

grad_zero_for_gs <r2d, r3d>

;===============================================================================
;	D/P               spec control          gradients	         
;===============================================================================
start, 	10u               freqRx(receive):f1
 	d1		  freqTx:f1             grad_ramp{0, 0, g0}	 	         
        (p0:sp0 ph1):f1
	d3                                      grad_ramp{g2, r2d*g3, g1+r3d*g4}
	d5		                        grad_off
	denab                       	        grad_ramp{g5, 0, 0} 
	                  ADC_INIT_(job0, ph1, ph0)  
	AQ_(job0)         ADC_START_(job0)
	d3                                      grad_ramp{g8, r2d*g6, r3d*g7} 		
	d6                                      grad_ramp{g8, 0, 0}
	d7		                        grad_off
        d0                ADC_END_(job0)               
	0u                                      grad_matrix.inc
        0u                freqTx.inc
        0u                freqRx.inc
lo to start times NSLICES
                          "l2=l2-1"
if "l2>=0" goto start
	0u		  ipp1                  grad_matrix.res
lo to start times NA
	0u                rpp1                  r2d.inc
lo to start times l0
	0u                                      r3d.inc
lo to start times l1
lo to start times NAE
SETUP_GOTO(start) 
exit
;===============================================================================
;phase lists

ph0 = 0
ph1 = 0 2 1 3
;===============================================================================
