;****************************************************************
;
; Copyright (c) 2002 - 2003
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
;
; CPMG - non-selective spectroscopy (pulse + acquire)
;
;****************************************************************


#include <MRI.include>
#include <PrepModulesHead.mod>

define delay d3mde
"d3mde = d3 - de"

define loopcounter lds = {$PVM_DummyScans}

INIT_DEVICES

;----------------------------------start of the main loop ----------
start,  UPDATE_DYN_PARS_30u
                subr TriggerPhase()

        (p0:sp0 ph0):f1
        d2

echoes, 10u

        if(SpoilerOnOff == On)
        {
                d4        grad_ramp{0, 0, g0}
                d5
                d4        grad_off
        }

        (p1:sp1 ph1):f1

        if(SpoilerOnOff == On)
        {
                d4        grad_ramp{0, 0, g0}
                d5
                d4        grad_off
        }

        10u
        d3mde
                        ADC_INIT_(job0, ph0, ph2)       ; duration = de
        AQ_(job0)       ADC_START_(job0)
        d3              ADC_END_(job0)

        lo to echoes times NEchoes

	d0

        "lds = lds - 1"                 ; this makes
        if "lds>=0" goto start          ; dummy scans

        0u              ipp0
        0u              ipp1

        lo to start times NA
        0u              rpp0
        0u              rpp1

        lo to start times NR
        SETUP_GOTO(start)

exit

ph0 = 0 2 1 3
ph1 = 1 3 2 0
ph2 = 0
