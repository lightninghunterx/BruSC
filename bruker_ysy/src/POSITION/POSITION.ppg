
;****************************************************************
;
; $Source$
;
; Copyright (c) 2005
; Bruker Biospin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
;
; POSITION.ppg
;
;****************************************************************


#include <MRI.include>

define list<frequency> freqTx = {$ACQ_O1_list}
define list<frequency> freqRx = {$ACQ_O1B_list}
define loopcounter lds={$NDummyScans}
define list<grad_vector> vec ={$GradVec}

define delay denab
"denab =  d4 - de"


INIT_DEVICES

start,          UPDATE_DYN_PARS_30u
       10u      freqRx(receive):f1
       10u      freqTx:f1

       (p0:sp0  ph0):f1                 ;non-sel 90

        d4      grad_ramp<magnet_coord>{ g0 * vec[]}
        d6                              ;tau = aq/2
        d4
        d4      grad_off
        d4
        d4      grad_ramp<magnet_coord>{g1 * vec[]}      ;spoiler
        d4
        d4      grad_off
        d4

       (p1:sp0  ph0):f1                  ;non-sel 180

        d4      grad_ramp<magnet_coord>{ g1* vec[]}     ;spoiler
        d4
        d4      grad_off
        d4
        d8
        d4                              ;for time symmetry
        d5                              ;d5 = d4/2
        d4      grad_ramp<magnet_coord>{g0 *vec[]}
        d5                              ;d5 = d4/2

        denab
                ADC_INIT_(job0, ph1,ph0)
  AQ_(job0)     ADC_START_(job0)
        d4      grad_off
       10u      ADC_END_(job0)
        d0

        "lds = lds -1"
        if "lds>=0" goto start

        0u     rpp0

        lo to start times NA
        0u     rpp0

        lo to start times NR


SETUP_GOTO(start)

exit

ph0 = 0
ph1 = 0
