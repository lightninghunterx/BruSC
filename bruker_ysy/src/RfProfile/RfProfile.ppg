;****************************************************************
;
; Copyright (c) 2005
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
; 
; RfProfile
;
;****************************************************************


#include <MRI.include>

define delay dur2
"dur2=d2-de"

define list<frequency> freqTx = {$ACQ_O1_list}
define list<frequency> freqRx = {$ACQ_O1B_list}
define loopcounter lds={$Ndummy}


INIT_DEVICES

;----------------- start of the main loop -----------------
start, 	UPDATE_DYN_PARS_30u
        10u  freqRx(receive):f1

;------------------ inversion/saturation ------------------
if(Preparation == Yes)
{
        d4      grad_ramp{g0, 0, 0}	  freqTx:f1     
       (p0:sp0  ph0):f1
        5m      grad_ramp{0, 60, 0}
        d4      grad_off
        d5
}

;----------------------- excitation -----------------------
if(ProfileMode == Excitation_Profile)
{
        d4      grad_ramp{g0, 0, 0}   
       (p0:sp0  ph0):f1
        d4      grad_off
}
else
{
        d4
       (p1:sp1  ph1):f1
        d4
}
      100u
        d4      grad_ramp{g1, 0, 0}
        d7
        d4      grad_off
        d1

;----------------------- refocuing ------------------------
if(ProfileMode == Excitation_Profile)
{
        d4
       (p1:sp1  ph1):f1
        d4 
}
if(ProfileMode == Refocusing_Profile)
{
        d4      grad_ramp{g0, 0, 0}
       (p0:sp0  ph0):f1
        d4      grad_off
}
if(Preparation == Yes)
{
        d4    
       (p2:sp1  ph1):f1
        d4    
}
        d3
        d4      grad_ramp{g0, 0, 0}
      dur2
                ADC_INIT_(job0, ph0, ph0)  
   AQ_(job0)    ADC_START_(job0)
       20u
        d4      grad_off
        1m
       10u      ADC_END_(job0)

	"lds = lds - 1"		; this makes
	if "lds>=0" goto start	; dummy scans 

       10u	ipp0
        d0
lo to start times NA

        0u	rpp0
lo to start times NR

SETUP_GOTO(start)

exit

ph0 = 0 
ph1 = 1
ph2 = 0 



	




