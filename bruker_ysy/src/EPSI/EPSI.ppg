;****************************************************************
;
; Copyright (c) 2001 - 2014
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
;
; EPSI -  echo planar imaging method
;
;****************************************************************
;
; d0 - TR padding
; d1 - refocusing
; d2 - TE padding
; d3 = RampTime + InterGradientWaitTime
; d4 = RiseTime
; d5 = spoiler
; d7 - TE padding (spin echo)

#include <MRI.include>
#include <PrepModulesHead.mod>
#include <epiHead.mod>
#include <DynamicShim.mod>

;counter of dummy scans
define loopcounter lds={$PVM_DummyScans}

define list<frequency> freqTx={$ACQ_O1_list}
define list<frequency> freqRx={$ACQ_O1B_list}

INIT_DEVICES

#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2

grad_zero_for_gs <r2d, r3d>


0u   grad_matrix.res

subr SatTransInit()
subr TriggerOutStart()

subr DynamicShimRes()

start,  0u

subr TriggerPhase()
subr TriggerOutVolume()
subr Tagging()
;----------------------------------start of the main loop ----------
slice,  UPDATE_DYN_PARS_30u
        subr DynamicShimApply()

        10u                 freqRx(receive):f1

subr TriggerSlice()
subr EpiGradSync()
subr TriggerOutSlice()


;----------------------------------preparation modules -------------
subr FovSat()
subr SatTrans()
subr FatSup()

        d5	grad_ramp{0, 0, g2}
;----------------------------------slice selection------------------
	d4 	grad_ramp{0, 0, g0} freqTx:f1
	(p0:sp0 	ph0):f1
	d3 	grad_off	    freqTx.inc
;----------------------------------slice rephase--------------------
	d1 	grad_ramp{0, 0, g1 }
	d4 	grad_off
if(PVM_SignalType == SignalType_Echo)
{
        d7
	d9	grad_ramp{0, 0, g3}
	d4      grad_ramp{0, 0, g0}
	(p1:sp1  ph1):f1
        d9	grad_ramp{0, 0, g3}
	d4	grad_off
}
;----------------------------------TE padding-----------------------
	d2


#include <epi.mod>

        subr DynamicShimInc()
     	d0  grad_matrix.inc
	lo to slice times NSLICES
        subr DynamicShimRes()
	0u  grad_matrix.res
        d6	;inter-volume delay

 ;------------------------------ dummy loop -------------------------
   "lds = lds - 1"
   if "lds >= 0"
   {
         0u       EpiVd.res
      goto start
   }

;----------------------------------averaging loop-------------------
        0u ipp0
	lo to start times NA
	0u rpp0 Interleaving.inc

	lo to start times PVM_EpiNShots

        0u Interleaving.res
;----------------------------------2d loop--------------------------
	0u r2d.inc
	lo to start times l1

;----------------------------------3d loop--------------------------
	0u r3d.inc
	lo to start times l2

;----------------------------------repetitions loop-------------------
        subr SatTransInc()
	lo to start times NR

SETUP_GOTO(start)

exit

ph0 = 0 2
ph1 = 0
