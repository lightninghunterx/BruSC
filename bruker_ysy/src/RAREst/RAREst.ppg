;****************************************************************
;
; Copyright (c) 2002
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
;
; a RARE imaging experiment
;
;****************************************************************


#include <MRI.include>
#include <PrepModulesHead.mod>

define list<frequency> freqTx = {$ACQ_O1_list}
define list<frequency> freqRx = {$ACQ_O1B_list}

define loopcounter lds = {$PVM_DummyScans}

define delay d3enab
"d3enab = d3 - de"

INIT_DEVICES

#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2

grad_zero_for_gs <r2d, r3d>

;pre-setting the r2d counter for the dummy scans:
decr,    0u           r2d.dec
     lo to decr times l1           ; PVM_DummyScans*PVM_RareFactor

         0u          r2d.dec       ; because igrad is done before gc_control

                subr SatTransInit()

start,   0u          r2d.store

acc,     0u
;----------------------- preparation modules -----------------------
                subr DriftComp()
                subr TriggerPhase()
                subr SliceSelIr()

slice,          UPDATE_DYN_PARS_30u
        10u          freqRx(receive):f1
                subr TriggerSlice()
                subr Tagging()
                subr FovSat()
                subr SatTrans()
                subr FatSup()

       10u           freqTx:f1
        d1           grad_ramp{0, 0, g4}          ; spoiler
        d4           grad_ramp{0, 0, g0}          ; slice grad
       (p0:sp0       ph0):f1

        d2           grad_ramp{g2, 0, g1}         ; dephasing
        d4           grad_ramp{ 0, 0, g3}         ; slice grad

echo1,  0u           r2d.restore

echo,   d8
        (p1:sp1       ph1):f1

        d8           r2d.inc
        d3           gc_control
       {             ;shapes cover one period between rf pulses
    
       d10           grad_shape{ReadShape()*100, PhaseShape()*100*r2d, SliceShape()*100}
                     grad_ramp{0, 0, g3}
       }

    d3enab
                     ADC_INIT_(job0, ph0, ph1)
        d6           ADC_START_(job0)
        d7           ADC_END_(job0)

     lo to echo times PVM_RareFactor

     lo to echo1 times PVM_NEchoImages
        2m
        d4           grad_off

;---------------------------- slice loop ---------------------------
        0u           grad_matrix.inc
        0u           freqTx.inc
        0u           freqRx.inc
        d0

     lo to slice times NSLICES

;--------------------------- dummy loop ----------------------------
                     "lds = lds - 1"                    ; this makes
        if "lds>=0" goto start                         ; dummy scans

;------------------------- averaging loop --------------------------
       d20
        0u           ipp0
     lo to acc times NA

;----------------------------- 2d-loop -----------------------------
        0u           rpp0
     lo to start times l2        ; PVM_EncMatrix[1] / PVM_RareFactor

;---------------------- motion averaging loop ----------------------
     lo to start times NAE

;-------------------------- sattrans loop --------------------------
   
                subr SatTransInc()

;------------------------- repetition loop -------------------------
                subr Evolution()

     lo to start times NR

SETUP_GOTO(start)

exit

ph0 = 0 2
ph1 = 1
