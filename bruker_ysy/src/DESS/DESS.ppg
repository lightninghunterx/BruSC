;****************************************************************
;
; Copyright (c) 2001 - 2003
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
; 
; DESS - a double echo imaging method
;
;****************************************************************


#include <MRI.include>

define delay denab 
define delay ded0  
define list<phase> phaselist = {$RFPhaseList}
define list<frequency> freqTx = {$ACQ_O1_list}
define list<frequency> freqRx = {$ACQ_O1B_list}

"denab =  d3 - de"
"ded0  =  d0 + d14"      

#include <PrepModulesHead.mod> 

INIT_DEVICES

#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2

grad_zero_for_gs <r2d, r3d>


start,   10u    freqRx(receive):f1

;----------------------------------start of the main loop ----------
slice, 	  0u

subr TriggerSlice()

;----------------------------------handle dummy scans --------------
if( l5 >= 1)   ;dummy scans
{
dummy,  30u
;----------------------------------slice selection------------------	
	 d3     grad_ramp{0, 0, g0}  freqTx:f1
	(p0:sp0 phaselist):f1
	 d3     grad_off
;--------------------slice rephase, read dephase, phase encoding----
	d10     grad_ramp{g2, r2d*g3, g1+r3d*g4}
	 d2     grad_off
;----------------------------------frequency encoding---------------
         d3     grad_ramp{g5, 0, 0}  
   AQ_(job0)
         d3     grad_ramp{g10, 0, g11} 
        d13                 
         d3     grad_ramp{g5, 0, 0}    
   AQ_(job0)
;----------------------------------read spoiler + phase encoding----
         d3     grad_off                                           
        d15     grad_ramp{g13, r2d*g7, g12+r3d*g8} ; 2nd and 3rd dims   
         d3     grad_off                                         
       ded0	
         0u     phaselist.inc    ;phase list 
        lo to dummy times l5
}

acquisition,    UPDATE_DYN_PARS_30u
;----------------------------------slice selection------------------	
	 d3     grad_ramp{0, 0, g0}  freqTx:f1
	(p0:sp0 phaselist):f1
	 d3     grad_off
;--------------------slice rephase, read dephase, phase encoding----
	d10     grad_ramp{g2, r2d*g3, g1+r3d*g4}
	 d2     grad_off
;----------------------------------frequency encoding---------------
      denab     grad_ramp{g5, 0, 0} 
                ADC_INIT_(job0, ph1, ph0)  
   AQ_(job0)    ADC_START_(job0)
         d3     grad_ramp{g10, 0, g11}  
        d13     ADC_END_(job0)             
      denab     grad_ramp{g5, 0, 0}     
                ADC_INIT_(job0, ph1, ph2)           
    AQ_(job0)   ADC_START_(job0)
;----------------------------------read spoiler + phase encoding----
         d3     grad_off                                          
        d15     grad_ramp{g13, r2d*g7, g12+r3d*g8} ; 2nd and 3rd dims  
         d3     grad_off                                          
       ded0     ADC_END_(job0)
         0u     phaselist.inc    ;phase list 
;----------------------------------2d loop--------------------------
	 0u     r2d.inc
	lo to acquisition times l1
;---------------------------slices loop-----------------------------	
	 0u     grad_matrix.inc  
         0u     freqTx.inc
         0u     freqRx.inc
	lo to slice times NSLICES
;----------------------------------3d loop--------------------------
	 0u     r3d.inc
	if (NSLICES < 2)
	{
         "l5=0"
        }
	lo to start times l2     
        lo to start times NAE
        subr Evolution()
        lo to start times NR 
SETUP_GOTO(start)

exit

ph0 = 0 
ph1 = 0 
ph2 = 2 ; RF 0-0




	





