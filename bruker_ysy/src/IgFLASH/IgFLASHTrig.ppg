;****************************************************************
;
; Copyright (c) 2011
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
; 
; IgFLASH - a self-gating gradient echo imaging method
;
;****************************************************************

#include <MRI.include>

define delay denab
"denab =  d3 - de -10u"
define delay d3m
"d3m = d3 - 10u"
define delay d10m
"d10m = d10 - 10u"
define delay d0half
"d0half=d0/2"
define delay d4m
"d4m = d4 -10u"
define delay del1
"del1 = 90u - de"

define list<phase> phaselist = {$RFPhaseList}
define list<frequency> freqTx = {$ACQ_O1_list}
define list<frequency> freqRx = {$ACQ_O1B_list}
define loopcounter dummy = {$PVM_DummyScans}
define loopcounter dumstart = {$PVM_DummyScans}

#include <PrepModulesHead.mod> 
#include <Navigator.mod>

subr FlowSatInit()
subr SatTransInit()

INIT_DEVICES

#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2

grad_zero_for_gs <r2d, r3d>

        
start,  5u
segm,   0u r2d.store
over,   0u r2d.restore
echo,   5u
if "dummy>0"
{
dummyloop, d0
;----------------------------------start of the main loop ----------
slice,  10u
;----------------------------------preparation modules -------------
subr FlowSat()
subr FovSat()
subr SatTrans()
subr FatSup()
        40u switch_realtime_tables  
subr NavigatorDummy(sp5,ph0)

        d6	grad_ramp{0, 0, g9}      
;----------------------------------slice selection------------------	
	d3 	grad_ramp{0, 0, g0} freqTx:f1
	(p0:sp0 phaselist):f1
	d4 	grad_off
if(PVM_NavPosMode != Nav_in_slice)
{
  if(FlowCompYesNo==No)
  {
	d10 	grad_ramp{g2, r2d*g3, g1+r3d*g4}
	d2 	grad_off
  }
  else
  {

        d10     grad_shape{RShape()*100, P2Shape()*100*r2d, SShape()*100 - P3Shape()*100*r3d}
  }
}
else
{
	d14   	grad_ramp{0, 0, g1}
        d3 	grad_off
        NAVD4    
        100u    ;additional delay
	d10	grad_ramp{g2, r2d*g3, r3d*g4}
	d2 	grad_off
}
;----------------------------------frequency encoding---------------
        d3      grad_ramp{g5, 0, 0} 
        AQ_(job0)
;----------------------------------read spoiler + phase encoding----
        d11     grad_ramp{g6, r2d*g7, r3d*g8} ; 2nd and 3rd dims
	d12     grad_ramp{g6, 0, 0}
	d3	grad_off
;----------------------------------slice and movie loop-------------	
if(AngioMode==No)
{
subr FlowSatInc()
	0u grad_matrix.inc  
        0u freqTx.inc
        0u freqRx.inc
	lo to slice times NSLICES
}
;----------------------------------2d loop--------------------------
        0u  phaselist.inc    ;phase list for RF spoiling
	lo to dummyloop times dummy
        "dummy=0"
}

        d0half
if trignl1 goto acq
        d0half

;----------------------------------start of the main loop ----------
slice1, 10u 
;----------------------------------preparation modules -------------
subr FlowSat()
subr FovSat()
subr SatTrans()
subr FatSup()
        40u switch_realtime_tables    
subr NavigatorDummy(sp5,ph0)

        d6	grad_ramp{0, 0, g9}      
;----------------------------------slice selection------------------	
	d3 	grad_ramp{0, 0, g0} freqTx:f1
	(p0:sp0 phaselist):f1
	d4 	grad_off
if(PVM_NavPosMode != Nav_in_slice)
{

  if(FlowCompYesNo==No)
  {
	d10 	grad_ramp{g2, r2d*g3, g1+r3d*g4}
	d2	grad_off
  }
  else
  {
        d10     grad_shape{RShape()*100, P2Shape()*100*r2d, SShape()*100 - P3Shape()*100*r3d}
  }
}
else
{
	d14   	grad_ramp{0, 0, g1}
        d3 	grad_off
        NAVD4    
        100u    ;additional delay
	d10	grad_ramp{g2, r2d*g3, r3d*g4}
	d2 	grad_off
}
;----------------------------------frequency encoding---------------
        d3      grad_ramp{g5, 0, 0} 
        AQ_(job0)
;----------------------------------read spoiler + phase encoding----
        d11     grad_ramp{g6, r2d*g7, r3d*g8} ; 2nd and 3rd dims
	d12     grad_ramp{g6, 0, 0}
	d3	grad_off
;----------------------------------slice and movie loop-------------	
if(AngioMode==No)
{
subr FlowSatInc()
	0u grad_matrix.inc
        0u freqTx.inc
        0u freqRx.inc
	lo to slice1 times NSLICES
}
;----------------------------------2d loop--------------------------
        0u  phaselist.inc    ;phase list for RF spoiling
	goto echo

acq,    d0half

;----------------------------------start of the main loop ----------
slice2, 10u 
;----------------------------------preparation modules -------------
subr FlowSat()
subr FovSat()
subr SatTrans()
subr FatSup()

        20u     SWITCH_(Navigator)
        20u     switch_realtime_tables  
subr Navigator(sp5,ph0,ph1)

        d6	grad_ramp{0, 0, g9}      
;----------------------------------slice selection------------------	
	d3 	grad_ramp{0, 0, g0} freqTx:f1
	(p0:sp0 phaselist):f1
if(PVM_NavPosMode != Nav_in_slice)
{
        10u 	grad_off
        d4m     SWITCH_(job0)
  if(FlowCompYesNo==No)
  {
	d10 	grad_ramp{g2, r2d*g3, g1+r3d*g4}
	d2 	grad_off
  }
  else
  {
        d10     grad_shape{RShape()*100, P2Shape()*100*r2d, SShape()*100 - P3Shape()*100*r3d} 
  }
}
else
{
	d4 	grad_off
	d14   	grad_ramp{0, 0, g1}     navrec(receive):f1
        d3 	grad_off
        ADC_INIT_(Navigator,ph1,phaselist)   ;de
        NAVD4   ADC_START_(Navigator)
        del1                                 ;additional delay
        10u     ADC_END_(Navigator)
	10u	grad_ramp{g2, r2d*g3, r3d*g4}
        d10m    SWITCH_(job0)
	d2	grad_off
     
}
;----------------------------------frequency encoding---------------
        10u     grad_ramp{g5, 0, 0} 
        denab   freqRx(receive):f1
        ADC_INIT_(job0,ph1,phaselist)	
        AQ_(job0) ADC_START_(job0)	
;----------------------------------read spoiler + phase encoding----
        d11     grad_ramp{g6, r2d*g7, r3d*g8} ; 2nd and 3rd dims
	d12     grad_ramp{g6, 0, 0}
	d3m	grad_off
        10u	ADC_END_(job0)
;----------------------------------slice and movie loop-------------	
if(AngioMode==No)
{
subr FlowSatInc()
	0u grad_matrix.inc  
        0u freqTx.inc
        0u freqRx.inc
	lo to slice2 times NSLICES
}
        0u phaselist.inc       ;phase list for RF spoiling
	0u r2d.inc

	lo to echo times l1     ; 2d loop 

        lo to over times l10    ; oversampling loop
      
        lo to segm times l11    ; segmentation loop 

;----------------------------Angio loop---------------------------
if(AngioMode==Yes)
{
if(NSLICES > 1)
{
subr FlowSatInc()
	0u grad_matrix.inc  
        0u freqTx.inc
        0u freqRx.inc
        "dummy=dumstart"
	lo to start times NSLICES
}
}
;----------------------------------3d loop--------------------------
	0u r3d.inc
	lo to start times l2

;----------------------------------sattrans loop--------------------

        subr SatTransInc()

;----------------------------------repetititon loop-----------------
        lo to start times NR
SETUP_GOTO(start)

exit

ph0 = 0
ph1 = 0 




	





