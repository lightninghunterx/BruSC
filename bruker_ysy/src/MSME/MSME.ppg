;****************************************************************
;
; Copyright (c) 2017
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
;
; a RARE imaging experiment
;
;****************************************************************
;
;

#include <MRI.include>
#include <PrepModulesHead.mod>

define delay denab
"denab = d3 - de"

define delay d5m40u
"d5m40u = d5 - 40u"

define list<frequency> freqTx = {$ACQ_O1_list}
define list<frequency> freqRx = {$ACQ_O1B_list}

INIT_DEVICES


#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2

grad_zero_for_gs <r2d, r3d>

              subr FlowSatInit()
              subr SatTransInit()
start,  0u
              subr DriftComp()
              subr TriggerPhase()
              subr SliceSelIr()
        
slice,             UPDATE_DYN_PARS_30u
       10u         freqRx(receive):f1
        0u         freqRx.inc

;---------------------- preparation modules ------------------------
              subr TriggerSlice()
              subr FlowSat()
              subr FovSat()
              subr SatTrans()
              subr FatSup()

;----------------------------- excitation --------------------------
        d9         grad_ramp{g1, 0, g1}                  ;TR spoiler
        d3         grad_ramp{ 0, 0, g0} freqTx:f1          ;slice on

       (p0:sp0     ph0):f1                                ;exc pulse

        d2         grad_ramp{g2, 0, g7}      ;read deph, slice spoil 
        d1         grad_off                               ;TE-fill 1

;--------------------------- refocusing ----------------------------
echo,   d3         grad_ramp{0, 0, g0}                     ;slice on

       (p1:sp1     ph1):f1                                 ;pi-pulse

        d5         grad_ramp{g8,  r2d*g4, g3-r3d*g6} ;enc+slice-reph
        d6         grad_off                              ;TE-fill 2a

     denab         grad_ramp{g5, 0, 0}                ;read-on + acq

                   ADC_INIT_(job0, ph0, ph1)  

  AQ_(job0)        ADC_START_(job0)

        d6         grad_off                              ;TE-fill 2b
    d5m40u         grad_ramp{g8, -r2d*g4, g3+r3d*g6};enc+slice-reph2
       40u         ADC_END_(job0)

    lo to echo times NECHOES

;----------------------------- flipback ----------------------------
   if( PVM_FlipBackOnOff == On)
   {

        d3         grad_ramp{0, 0, g0}                     ;slice on
       (p1:sp1     ph1):f1                                 ;pi-pulse
        d1         grad_off
        d2         grad_ramp{g2, 0,  0}                ;read-dephase
        d3         grad_ramp{ 0, 0, g0}                    ;slice on
       (p0:sp0     ph3):f1                                 ;flipback
   }	
        d3         grad_off

;---------------------------- slice loop ---------------------------
        0u         grad_matrix.inc
        d0         freqTx.inc

              subr FlowSatInc()

    lo to slice times NSLICES

       d10         freqTx.res 
        0u         freqRx.res
        0u         ipp0
        0u         ipp3

;--------------------------- dummy loop  ----------------------------
                  "l3=l3-1"
   if "l3>=0" goto start

;------------------------- averaging loop --------------------------
    lo to start times NA

        0u         rpp0
        0u         rpp3

;----------------------------- common-loop (2D/3D) -----------------------------
        0u        r2d.inc
        0u        r3d.inc
     lo to start times l1

;---------------------- motion averaging loop ----------------------
    lo to start times NAE

;--------------------------- sattrans loop -------------------------
              subr SatTransInc()

;------------------------ repetititon loop  ------------------------
              subr Evolution()
    lo to start times NR

SETUP_GOTO(start)

exit

ph0 = 0 2
ph1 = 1
ph3 = 2 0
