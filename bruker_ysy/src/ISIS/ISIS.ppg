;*************************************************************************
; $Source$
;
; Copyright (c) 2007-2008
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; $Id$
;

#include <MRI.include>

define loopcounter dscount
"dscount=l3"

define loopcounter cycle

define list<loopcounter> avList={$AverageList}

define delay d14m20
"d14m20=d14-20u" ;compensates for 20u delay (including de) in acqdec

define list<frequency> freqExc={$ACQ_O2_list}
define list<frequency> freqRx={$ACQ_O1B_list}

#include <PrepModulesHead.mod>
#include <acqdec.mod>
#include <ISIS_Head.mod>


INIT_DEVICES

"cycle = 0"

        0u      rpp1
        0u	rpp2

        subr SatTransInit()

start,	UPDATE_DYN_PARS_30u
	1m freqRx(receive):f1

        ;--- prep modules
        subr TriggerSlice()
        subr SatTrans()
        subr Noe()
        subr ADDOVS(ph0)
        subr WsOvs(ph0,ph0)

	;--- ISIS inversions
        subr inversions(cycle)

        ;--- excitation
	d13	freqExc:f1
	(p0:sp0 ph1):f1
	d14m20  freqExc.inc

	;--- acquisition
        subr acqdec(ph0,ph2,AQ_(job0))	; acquire FID with decoupling (lasts aqq+20u)
        1m ADC_END_(job0)

        if(ACQ_scan_type == Scan_Experiment)
        {
	  d0 ipp1
	  0u ipp2

          "cycle = cycle+1"
          if "cycle > 7"
          {
            "cycle=0"
          }
        }
        else   ;cycle=0 in setup mode (no inversions)
        {
          d0
        }

	"dscount = dscount-1"
	if "dscount >= 0" goto start  ; dummy scans

	lo to start times avList
	0u      avList.inc

	0u	rpp1
	0u	rpp2

        subr SatTransInc()
	lo to start times NR

SETUP_GOTO(start)

exit

ph0 =  0
ph1 = {0 0 0 0 0 0 0 0}^2^1^3
ph2 = {0 0 2 0 2 2 0 2}^2^1^3

